Перем МинимальнаяДатаНачала;
Перем МаксимальнаяДатаОкончания;

Процедура ОбработкаПроведения(Отказ, Режим)

	Движения.ВКМ_ОсновныеНачисления.Записывать = Истина;
	Движения.ВКМ_ДополнительныеНачисления.Записывать = Истина;
	Движения.ВКМ_Удержания.Записывать = Истина;

	СформироватьДвижениеПоОкладу();
	СформироватьДвижениеПоОтпуску();
	СформироватьСторноЗаписи();
	РассчитатьОклад();
	РассчитатьОтпуск();
	СформироватьДополнительныеНачисления();

	СформироватьДвижениеВзаиморасчетыССотрудниками();

КонецПроцедуры

Процедура СформироватьДвижениеПоОкладу()

	Отбор = Новый Структура;
	Отбор.Вставить("ВКМ_ВидРасчета", ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Оклад);

	СтрокаОклада = ВКМ_Начисления.НайтиСтроки(Отбор);

	Если СтрокаОклада.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	МинимальнаяДатаНачала = СтрокаОклада[0].ВКМ_ДатаНачала;
	МаксимальнаяДатаОкончания = СтрокаОклада[0].ВКМ_ДатаОкончания;

	Для Сч = 1 По СтрокаОклада.Количество() - 1 Цикл

		Если МинимальнаяДатаНачала > СтрокаОклада[Сч].ВКМ_ДатаНачала Тогда
			МинимальнаяДатаНачала = СтрокаОклада[Сч].ВКМ_ДатаНачала;
		КонецЕсли;

		Если МаксимальнаяДатаОкончания < СтрокаОклада[Сч].ВКМ_ДатаОкончания Тогда
			МаксимальнаяДатаОкончания = СтрокаОклада[Сч].ВКМ_ДатаОкончания;
		КонецЕсли;

	КонецЦикла;

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВКМ_НачисленияЗарплатыНачисления.ВКМ_Сотрудник КАК Сотрудник,
	|	ВКМ_НачисленияЗарплатыНачисления.ВКМ_Сотрудник.ВКМ_КатегорияСотрудника КАК СотрудникКатегорияСотрудника,
	|	ВКМ_НачисленияЗарплатыНачисления.ВКМ_ВидРасчета КАК ВидРасчета,
	|	ВКМ_НачисленияЗарплатыНачисления.ВКМ_ДатаНачала КАК ДатаНачала,
	|	ВКМ_НачисленияЗарплатыНачисления.ВКМ_ДатаОкончания КАК ДатаОкончания,
	|	ВКМ_НачисленияЗарплатыНачисления.ВКМ_ГрафикРаботы КАК ГрафикРаботы
	|ПОМЕСТИТЬ ВТ_Документ
	|ИЗ
	|	Документ.ВКМ_НачисленияЗарплаты.ВКМ_Начисления КАК ВКМ_НачисленияЗарплатыНачисления
	|ГДЕ
	|	ВКМ_НачисленияЗарплатыНачисления.Ссылка = &Ссылка
	|	И ВКМ_НачисленияЗарплатыНачисления.ВКМ_ВидРасчета = &Оклад
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВКМ_УсловияОплатыСотрудниковСрезПоследних.Период КАК Период,
	|	ВКМ_УсловияОплатыСотрудниковСрезПоследних.ВКМ_Сотрудник КАК Сотрудник,
	|	ВКМ_УсловияОплатыСотрудниковСрезПоследних.ВКМ_Оклад КАК Оклад
	|ПОМЕСТИТЬ ВТ_ИзменениеОклада
	|ИЗ
	|	РегистрСведений.ВКМ_УсловияОплатыСотрудников.СрезПоследних(&МинимальнаяДатаНачала, ВКМ_Сотрудник В
	|		(ВЫБРАТЬ
	|			ВТ_Документ.Сотрудник КАК Сотрудник
	|		ИЗ
	|			ВТ_Документ КАК ВТ_Документ)) КАК ВКМ_УсловияОплатыСотрудниковСрезПоследних
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВКМ_УсловияОплатыСотрудников.Период,
	|	ВКМ_УсловияОплатыСотрудников.ВКМ_Сотрудник,
	|	ВКМ_УсловияОплатыСотрудников.ВКМ_Оклад
	|ИЗ
	|	РегистрСведений.ВКМ_УсловияОплатыСотрудников КАК ВКМ_УсловияОплатыСотрудников
	|ГДЕ
	|	ВКМ_УсловияОплатыСотрудников.Период > &МинимальнаяДатаНачала
	|	И ВКМ_УсловияОплатыСотрудников.Период <= &МаксимальнаяДатаОкончания
	|	И ВКМ_УсловияОплатыСотрудников.ВКМ_Сотрудник В
	|		(ВЫБРАТЬ
	|			ВТ_Документ.Сотрудник КАК Сотрудник
	|		ИЗ
	|			ВТ_Документ КАК ВТ_Документ)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДатаНачала.Период КАК ДатаНачала,
	|	ДатаНачала.Сотрудник КАК Сотрудник,
	|	МИНИМУМ(ДатаНачала.Оклад) КАК Оклад,
	|	ЕСТЬNULL(ДОБАВИТЬКДАТЕ(ДатаОкончания.Период, СЕКУНДА, -1), ДАТАВРЕМЯ(3999, 12, 31)) КАК ДатаОкончания
	|ПОМЕСТИТЬ ВТ_ИнтервалыОклада
	|ИЗ
	|	ВТ_ИзменениеОклада КАК ДатаНачала
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ИзменениеОклада КАК ДатаОкончания
	|		ПО ДатаНачала.Сотрудник = ДатаОкончания.Сотрудник
	|		И ДатаНачала.Период < ДатаОкончания.Период
	|СГРУППИРОВАТЬ ПО
	|	ДатаНачала.Период,
	|	ДатаНачала.Сотрудник,
	|	ЕСТЬNULL(ДОБАВИТЬКДАТЕ(ДатаОкончания.Период, СЕКУНДА, -1), ДАТАВРЕМЯ(3999, 12, 31))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Документ.Сотрудник КАК Сотрудник,
	|	ВТ_Документ.ВидРасчета КАК ВидРасчета,
	|	ВТ_ИнтервалыОклада.Оклад КАК Показатель,
	|	ВЫБОР
	|		КОГДА ВТ_ИнтервалыОклада.ДатаНачала > ВТ_Документ.ДатаНачала
	|			ТОГДА ВТ_ИнтервалыОклада.ДатаНачала
	|		ИНАЧЕ ВТ_Документ.ДатаНачала
	|	КОНЕЦ КАК ПериодДействияНачало,
	|	ВЫБОР
	|		КОГДА ВТ_ИнтервалыОклада.ДатаОкончания < ВТ_Документ.ДатаОкончания
	|			ТОГДА КОНЕЦПЕРИОДА(ВТ_ИнтервалыОклада.ДатаОкончания, ДЕНЬ)
	|		ИНАЧЕ КОНЕЦПЕРИОДА(ВТ_Документ.ДатаОкончания, ДЕНЬ)
	|	КОНЕЦ КАК ПериодДействияКонец,
	|	ВТ_Документ.ГрафикРаботы КАК ГрафикРаботы
	|ИЗ
	|	ВТ_Документ КАК ВТ_Документ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ИнтервалыОклада КАК ВТ_ИнтервалыОклада
	|		ПО ВТ_Документ.Сотрудник = ВТ_ИнтервалыОклада.Сотрудник
	|		И ВТ_Документ.ДатаНачала <= ВТ_ИнтервалыОклада.ДатаОкончания
	|		И ВТ_Документ.ДатаОкончания >= ВТ_ИнтервалыОклада.ДатаНачала";

	Запрос.УстановитьПараметр("Оклад", ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Оклад);
	Запрос.УстановитьПараметр("МинимальнаяДатаНачала", МинимальнаяДатаНачала);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("МаксимальнаяДатаОкончания", МаксимальнаяДатаОкончания);

	РезультатЗапроса = Запрос.Выполнить();

	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();

	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Движение = Движения.ВКМ_ОсновныеНачисления.Добавить();
		//ЗаполнитьЗначенияСвойств(Движение, ВыборкаДетальныеЗаписи);
		Движение.ВидРасчета = ВыборкаДетальныеЗаписи.ВидРасчета;
		Движение.ВКМ_ГрафикРаботы = ВыборкаДетальныеЗаписи.ГрафикРаботы;
		Движение.ПериодДействияНачало = ВыборкаДетальныеЗаписи.ПериодДействияНачало;
		Движение.ПериодДействияКонец = ВыборкаДетальныеЗаписи.ПериодДействияКонец;
		Движение.ПериодРегистрации = Дата;
		Движение.ВКМ_Сотрудник = ВыборкаДетальныеЗаписи.Сотрудник;
		Движение.ВКМ_Показатель = ВыборкаДетальныеЗаписи.Показатель;
		Движение.БазовыйПериодНачало = НачалоМесяца(ДобавитьМесяц(ВыборкаДетальныеЗаписи.ПериодДействияНачало, -12));
			Движение.БазовыйПериодКонец = КонецМесяца(ДобавитьМесяц(ВыборкаДетальныеЗаписи.ПериодДействияКонец, -1));
		Движение.Сторно = Ложь;
	КонецЦикла;

	Движения.ВКМ_ОсновныеНачисления.Записать();
КонецПроцедуры

Процедура СформироватьДвижениеПоОтпуску()

	Для Каждого ТекСтрокаНачисления Из ВКМ_Начисления Цикл
		Если ТекСтрокаНачисления.ВКМ_ВидРасчета = ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Отпуск Тогда
			Движение = Движения.ВКМ_ОсновныеНачисления.Добавить();
			Движение.Сторно = Ложь;
			Движение.ВидРасчета = ТекСтрокаНачисления.ВКМ_ВидРасчета;
			Движение.ПериодДействияНачало = ТекСтрокаНачисления.ВКМ_ДатаНачала;
			Движение.ПериодДействияКонец = КонецДня(ТекСтрокаНачисления.ВКМ_ДатаОкончания);
			Движение.ПериодРегистрации = Дата;
			Движение.БазовыйПериодНачало = НачалоМесяца(ДобавитьМесяц(ТекСтрокаНачисления.ВКМ_ДатаНачала, -12));
			Движение.БазовыйПериодКонец = КонецМесяца(ДобавитьМесяц(ТекСтрокаНачисления.ВКМ_ДатаОкончания, -1));
			Движение.ВКМ_Сотрудник = ТекСтрокаНачисления.ВКМ_Сотрудник;
			Движение.ВКМ_ГрафикРаботы = ТекСтрокаНачисления.ВКМ_ГрафикРаботы;
		КонецЕсли;
	КонецЦикла;

	Движения.ВКМ_ОсновныеНачисления.Записать();

КонецПроцедуры

Процедура СформироватьСторноЗаписи()

	СторноЗаписи = Движения.ВКМ_ОсновныеНачисления.ПолучитьДополнение();

	Если Не ЗначениеЗаполнено(СторноЗаписи) Тогда
		Возврат;
	КонецЕсли;

	Для Каждого Запись Из СторноЗаписи Цикл
		Движение = Движения.ВКМ_ОсновныеНачисления.Добавить();
		ЗаполнитьЗначенияСвойств(Движение, Запись);
		Движение.ПериодРегистрации = Дата;
		Движение.ПериодДействияНачало = Запись.ПериодДействияНачалоСторно;
		Движение.ПериодДействияКонец = Запись.ПериодДействияКонецСторно;
		Движение.ВКМ_Результат = 0;
		Движение.ВКМ_ОтработаноДней = 0;
		Движение.Сторно = Истина;
	КонецЦикла;

	Движения.ВКМ_ОсновныеНачисления.Записать();

КонецПроцедуры

Процедура РассчитатьОклад()

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВКМ_ОсновныеНачисленияДанныеГрафика.НомерСтроки,
	|	ВКМ_ОсновныеНачисленияДанныеГрафика.ВКМ_ЗначениеПериодДействия КАК План,
	|	ВКМ_ОсновныеНачисленияДанныеГрафика.ВКМ_ЗначениеФактическийПериодДействия КАК Факт
	|ИЗ
	|	РегистрРасчета.ВКМ_ОсновныеНачисления.ДанныеГрафика(Регистратор.Ссылка = &Ссылка
	|	И ВидРасчета = &Оклад) КАК ВКМ_ОсновныеНачисленияДанныеГрафика";

	Запрос.УстановитьПараметр("Оклад", ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Оклад);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);

	РезультатЗапроса = Запрос.Выполнить();

	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();

	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Движение = Движения.ВКМ_ОсновныеНачисления[ВыборкаДетальныеЗаписи.НомерСтроки - 1];
		Движение.ВКМ_Результат = Движение.ВКМ_Показатель * ВыборкаДетальныеЗаписи.Факт / ВыборкаДетальныеЗаписи.План;
		Движение.ВКМ_ОтработаноДней = ВыборкаДетальныеЗаписи.Факт / 8;

		Если Движение.Сторно Тогда
			Движение.ВКМ_Результат = -Движение.ВКМ_Результат;
			Движение.ВКМ_ОтработаноДней = -Движение.ВКМ_ОтработаноДней;
		КонецЕсли;
		
		//Удержание
		ДвижениеНДФЛ = Движения.ВКМ_Удержания.Добавить();
		ДвижениеНДФЛ.ПериодРегистрации = Дата;
		ДвижениеНДФЛ.ВидРасчета = ПланыВидовРасчета.ВКМ_Удержания.НДФЛ;
		ДвижениеНДФЛ.ВКМ_Сотрудник = Движение.ВКМ_Сотрудник;
		ДвижениеНДФЛ.ПериодДействияНачало = Движение.ПериодДействияНачало;
		ДвижениеНДФЛ.ПериодДействияКонец = Движение.ПериодДействияКонец;
		ДвижениеНДФЛ.ВКМ_Результат = Движение.ВКМ_Результат * 13 / 100;
		ДвижениеНДФЛ.ПериодДействияНачало = МинимальнаяДатаНачала;
		ДвижениеНДФЛ.ПериодДействияКонец = МаксимальнаяДатаОкончания;
	КонецЦикла;
	Движения.ВКМ_ОсновныеНачисления.Записать( , Истина);
	Движения.ВКМ_Удержания.Записать();

КонецПроцедуры

Процедура РассчитатьОтпуск()

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВКМ_ОсновныеНачисленияДанныеГрафика.ВКМ_Сотрудник,
	|	ВКМ_ОсновныеНачисленияДанныеГрафика.БазовыйПериодНачало,
	|	ВКМ_ОсновныеНачисленияДанныеГрафика.БазовыйПериодКонец,
	|	ВКМ_ОсновныеНачисленияДанныеГрафика.НомерСтроки,
	|	ЕСТЬNULL(ВКМ_ОсновныеНачисленияДанныеГрафика.ВКМ_ЗначениеФактическийПериодДействия, 0) КАК
	|		ЗначениеФактическийПериодДействия
	|ПОМЕСТИТЬ ВТ_Документ
	|ИЗ
	|	РегистрРасчета.ВКМ_ОсновныеНачисления.ДанныеГрафика(Регистратор.Ссылка = &Ссылка
	|	И ВидРасчета = &Отпуск) КАК ВКМ_ОсновныеНачисленияДанныеГрафика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВКМ_ОсновныеНачисленияДанныеГрафика.ВКМ_Сотрудник,
	|	ВКМ_ОсновныеНачисленияДанныеГрафика.НомерСтроки
	|ПОМЕСТИТЬ ВТ_ДокументОтпуск
	|ИЗ
	|	РегистрРасчета.ВКМ_ОсновныеНачисления.ДанныеГрафика(Регистратор.Ссылка = &Ссылка
	|	И ВидРасчета = &Отпуск) КАК ВКМ_ОсновныеНачисленияДанныеГрафика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Документ.ВКМ_Сотрудник,
	|	ВТ_Документ.НомерСтроки,
	|	ВТ_Документ.ЗначениеФактическийПериодДействия КАК Факт,
	|	СУММА(ЕСТЬNULL(ВКМ_ОсновныеНачисленияДанныеГрафика.ВКМ_Результат, 0)) КАК Результат,
	|	СУММА(ЕСТЬNULL(ВКМ_ОсновныеНачисленияДанныеГрафика.ВКМ_ЗначениеФактическийПериодДействия, 0)) КАК РабЧасы
	|ИЗ
	|	ВТ_Документ КАК ВТ_Документ
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ВКМ_ОсновныеНачисления.ДанныеГрафика(ВидРасчета = &Оклад) КАК
	|			ВКМ_ОсновныеНачисленияДанныеГрафика
	|		ПО ВТ_Документ.ВКМ_Сотрудник = ВКМ_ОсновныеНачисленияДанныеГрафика.ВКМ_Сотрудник
	|		И ВКМ_ОсновныеНачисленияДанныеГрафика.ПериодДействия
	|			МЕЖДУ ВТ_Документ.БазовыйПериодНачало И ВТ_Документ.БазовыйПериодКонец
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Документ.ВКМ_Сотрудник,
	|	ВТ_Документ.НомерСтроки,
	|	ВТ_Документ.ЗначениеФактическийПериодДействия";

	Запрос.УстановитьПараметр("Отпуск", ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Отпуск);
	Запрос.УстановитьПараметр("Оклад", ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Оклад);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);

	РезультатЗапроса = Запрос.Выполнить();

	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();

	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Движение = Движения.ВКМ_ОсновныеНачисления[ВыборкаДетальныеЗаписи.НомерСтроки - 1];
		Движение.ВКМ_Результат = ВыборкаДетальныеЗаписи.Результат * ВыборкаДетальныеЗаписи.Факт / ВыборкаДетальныеЗаписи.РабЧасы;
		Движение.ВКМ_ОтработаноДней = ВыборкаДетальныеЗаписи.Факт / 8;

		Если Движение.Сторно Тогда
			Движение.ВКМ_Результат = -Движение.ВКМ_Результат;
			Движение.ВКМ_ОтработаноДней = -Движение.ВКМ_ОтработаноДней;
		КонецЕсли;
	
		ДвижениеНДФЛ = Движения.ВКМ_Удержания.Добавить();
		ДвижениеНДФЛ.ПериодРегистрации = Дата;
		ДвижениеНДФЛ.ВидРасчета = ПланыВидовРасчета.ВКМ_Удержания.НДФЛ;
		ДвижениеНДФЛ.ВКМ_Сотрудник = Движение.ВКМ_Сотрудник;
		ДвижениеНДФЛ.ПериодДействияНачало = Движение.ПериодДействияНачало;
		ДвижениеНДФЛ.ПериодДействияКонец = Движение.ПериодДействияКонец;
		ДвижениеНДФЛ.ВКМ_Результат = Движение.ВКМ_Результат * 13 / 100;
		ДвижениеНДФЛ.ПериодДействияНачало = МинимальнаяДатаНачала;
		ДвижениеНДФЛ.ПериодДействияКонец = МаксимальнаяДатаОкончания;
	КонецЦикла;

	Движения.ВКМ_ОсновныеНачисления.Записать( , Истина);
	Движения.ВКМ_Удержания.Записать();

КонецПроцедуры

Процедура СформироватьДополнительныеНачисления()

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВКМ_ВыполненныеСотрудникомРаботыОбороты.ВКМ_Сотрудник,
	|	СУММА(ВКМ_ВыполненныеСотрудникомРаботыОбороты.ВКМ_ЧасовКОплатеОборот) КАК ЧасовКОплате,
	|	СУММА(ВКМ_ВыполненныеСотрудникомРаботыОбороты.ВКМ_СуммаКОплатеОборот) КАК СуммаКОплате
	|ИЗ
	|	РегистрНакопления.ВКМ_ВыполненныеСотрудникомРаботы.Обороты(&МинимальнаяДатаНачала, &МаксимальнаяДатаОкончания,
	|		Регистратор, ВКМ_Сотрудник.ВКМ_КатегорияСотрудника = &КатегорияСотрудников) КАК
	|		ВКМ_ВыполненныеСотрудникомРаботыОбороты
	|СГРУППИРОВАТЬ ПО
	|	ВКМ_ВыполненныеСотрудникомРаботыОбороты.ВКМ_Сотрудник";

	Запрос.УстановитьПараметр("МинимальнаяДатаНачала", МинимальнаяДатаНачала);
	Запрос.УстановитьПараметр("МаксимальнаяДатаОкончания", КонецДня(МаксимальнаяДатаОкончания));
	Запрос.УстановитьПараметр("КатегорияСотрудников", Справочники.ВКМ_КатегорииСпециалистов.НайтиПоНаименованию("Специалист"));

	РезультатЗапроса = Запрос.Выполнить();

	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();

	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Движение = Движения.ВКМ_ДополнительныеНачисления.Добавить();
		Движение.ВидРасчета = ПланыВидовРасчета.ВКМ_ДополнительныеНачисления.Премия;
		Движение.ПериодРегистрации = Дата;
		Движение.ВКМ_Сотрудник = ВыборкаДетальныеЗаписи.ВКМ_Сотрудник;

		Движение.ПериодДействияНачало = МинимальнаяДатаНачала;
		Движение.ПериодДействияКонец = КонецДня(МаксимальнаяДатаОкончания);
		Движение.ВКМ_Результат = ВыборкаДетальныеЗаписи.СуммаКОплате;
		Движение.ВКМ_ОтработаноЧасов = ВыборкаДетальныеЗаписи.ЧасовКОплате;
	
		//Удержание
		ДвижениеНДФЛ = Движения.ВКМ_Удержания.Добавить();
		ДвижениеНДФЛ.ПериодРегистрации = Дата;
		ДвижениеНДФЛ.ВидРасчета = ПланыВидовРасчета.ВКМ_Удержания.НДФЛ;
		ДвижениеНДФЛ.ВКМ_Сотрудник = Движение.ВКМ_Сотрудник;
		ДвижениеНДФЛ.ПериодДействияНачало = Движение.ПериодДействияНачало;
		ДвижениеНДФЛ.ПериодДействияКонец = Движение.ПериодДействияКонец;
		ДвижениеНДФЛ.ВКМ_Результат = Движение.ВКМ_Результат * 13 / 100;
		ДвижениеНДФЛ.ПериодДействияНачало = МинимальнаяДатаНачала;
		ДвижениеНДФЛ.ПериодДействияКонец = МаксимальнаяДатаОкончания;
	КонецЦикла;

	Движения.ВКМ_ДополнительныеНачисления.Записать();
	Движения.ВКМ_Удержания.Записать();

КонецПроцедуры

Процедура СформироватьДвижениеВзаиморасчетыССотрудниками()

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВКМ_ОсновныеНачисления.ВКМ_Сотрудник,
	|	ВКМ_ОсновныеНачисления.ВКМ_Результат КАК ОсновныеНачисления,
	|	0 КАК ДополнительныеНачисления,
	|	0 КАК Удержание,
	|	ВКМ_ОсновныеНачисления.Регистратор
	|ПОМЕСТИТЬ ВТ_ДвижениеПоРегистру
	|ИЗ
	|	РегистрРасчета.ВКМ_ОсновныеНачисления КАК ВКМ_ОсновныеНачисления
	|ГДЕ
	|	ВКМ_ОсновныеНачисления.Регистратор = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВКМ_ДополнительныеНачисления.ВКМ_Сотрудник,
	|	0,
	|	ВКМ_ДополнительныеНачисления.ВКМ_Результат,
	|	0,
	|	ВКМ_ДополнительныеНачисления.Регистратор
	|ИЗ
	|	РегистрРасчета.ВКМ_ДополнительныеНачисления КАК ВКМ_ДополнительныеНачисления
	|ГДЕ
	|	ВКМ_ДополнительныеНачисления.Регистратор = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВКМ_Удержания.ВКМ_Сотрудник,
	|	СУММА(0),
	|	СУММА(0),
	|	СУММА(ВКМ_Удержания.ВКМ_Результат),
	|	ВКМ_Удержания.Регистратор
	|ИЗ
	|	РегистрРасчета.ВКМ_Удержания КАК ВКМ_Удержания
	|ГДЕ
	|	ВКМ_Удержания.Регистратор = &Ссылка
	|СГРУППИРОВАТЬ ПО
	|	ВКМ_Удержания.ВКМ_Сотрудник,
	|	ВКМ_Удержания.Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ДвижениеПоРегистру.Регистратор,
	|	ВТ_ДвижениеПоРегистру.ВКМ_Сотрудник,
	|	СУММА(ВТ_ДвижениеПоРегистру.ОсновныеНачисления) КАК ОсновныеНачисления,
	|	СУММА(ВТ_ДвижениеПоРегистру.ДополнительныеНачисления) КАК ДополнительныеНачисления,
	|	СУММА(ВТ_ДвижениеПоРегистру.Удержание) КАК Удержание
	|ИЗ
	|	ВТ_ДвижениеПоРегистру КАК ВТ_ДвижениеПоРегистру
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ДвижениеПоРегистру.Регистратор,
	|	ВТ_ДвижениеПоРегистру.ВКМ_Сотрудник";

	Запрос.УстановитьПараметр("Ссылка", Ссылка);

	РезультатЗапроса = Запрос.Выполнить();

	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();

	НаборЗаписейВзаиморасчетыССотрудниками = РегистрыНакопления.ВКМ_ВзаиморасчетыССотрудниками.СоздатьНаборЗаписей();
	НаборЗаписейВзаиморасчетыССотрудниками.Отбор.Регистратор.Установить(Ссылка);
	НаборЗаписейВзаиморасчетыССотрудниками.Прочитать();
	НаборЗаписейВзаиморасчетыССотрудниками.Очистить();

	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл

		Запись = НаборЗаписейВзаиморасчетыССотрудниками.ДобавитьПриход();
		ЗаполнитьЗначенияСвойств(Запись, ВыборкаДетальныеЗаписи);
		Запись.Период = Дата;
		Запись.ВКМ_Сумма = ВыборкаДетальныеЗаписи.ОсновныеНачисления + ВыборкаДетальныеЗаписи.ДополнительныеНачисления
			- ВыборкаДетальныеЗаписи.Удержание;

	КонецЦикла;
	НаборЗаписейВзаиморасчетыССотрудниками.Записать();

КонецПроцедуры